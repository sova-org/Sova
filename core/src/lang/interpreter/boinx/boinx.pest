WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }

note_letter = { 'A'..'G' }
letter = _{ ASCII_ALPHA }
digit = _{ ASCII_DIGIT }
int = @{ digit+ }

escape = { "\\" }

seq_var = { "$" }
env_func = { "_" }
qualif = _{ seq_var | env_func }

sharp = { "#" }
flat = { "b" }

compo_op = { "|" }
iter_op = { "°" }
each_op = { "~" }
zip_op = { "!" }
super_each_op = { "#" }

cond_op = _{ "?" }
else_op = _{ ":" }

note = ${ note_letter ~ (sharp | flat)* ~ int? ~ !letter }

real = @{ int ~ "." ~ int }
number = _{ real | int }
micros = ${ int ~ "u" } 
semibeats = ${ number ~ "''" } 
beats = ${ number ~ "'" } 

str = @{ 
	("\"" ~ (!"\"" ~ ANY)* ~ "\"") |
    ("'" ~ (!"'" ~ ANY)* ~ "'") 
}

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
rem = { "-" }
shl = { "<<" }
shr = { ">>" }
pow = { "^" }

bin_op = _{ add | sub | mul | div | rem | shl | shr | pow }

minus = { "-" }
un_op = _{ minus }

lt = { "<" }
le = { "<=" }
eq = { "==" }
ge = { ">=" }
gt = { ">" }
neq = { "!=" }

comp_op = _{ lt | le | eq | ge | gt | neq }

name = { (letter | digit | "_")+ }
ident = ${ !int ~ !note ~ qualif? ~ name }

duration = _{ micros | semibeats | beats }

mute = { "_" }
placeholder = { "." }

sub_prog = { "{" ~ block ~ "}" }

ci = { item }
cond = { ci ~ comp_op ~ ci }

if_else = { cond_op ~ cond ~ sub_prog ~ else_op ~ sub_prog }
el = _{ sub_prog | simul | seque | map | if_else | placeholder | note | duration | number | ident | str | mute }

item = _{ un_op? ~ escape? ~ el ~ (bin_op ~ item)? }

li = { item }

seque = { "[" ~ li+ ~ "]" }
simul = { "(" ~ li+ ~ ")" }
map = { "<" ~ (name ~ ":" ~ li)+ ~ ">" }

compo = { item ~ ( (zip_op | compo_op | iter_op | each_op | super_each_op) ~ item )* }

dev = { !":" ~ item }
chan = { item }
target = _{ dev ~ (":" ~ chan)? }

output = { compo ~ ("@" ~ target)? }
assign = { ident ~ "=" ~ compo }

statement = _{ assign | output }
block = _{ statement+ }

prog = _{ SOI ~ block? ~ EOI }

/*
Example track:

chords = [ C4 A3 F3 G5 ]

maj = (. .+4 .+7)
min = (. .+3 .+7)

chords ° [ maj min maj maj ] ~ [[. .] . [. .] .] @ v : 1

chords ~ (. - 12) ~ [....] ° [....] @ a: 2

kick = 1u snare = 2 hh = 3  

kick | [ . _ ] @ c: 3
snare | [ _ . ] 
hh | [....] @ c

after = [_ .] ° [. 1.0]

(pattern delay) ° after

[ (C E G) D [ E E ] F ]

(kick snare) ~ (. hh) ° [ . hh . hh . hh .  hh]

[C4 A4 E3] ~ <"n" . "reverb" 4>

*/
