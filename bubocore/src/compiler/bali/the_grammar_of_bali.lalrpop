use crate::compiler::bali::bali_ast::{BaliProgram, TopLevelEffect, Statement, Effect, Expression, Value, Fraction, ConcreteFraction, BooleanExpression};

grammar;

pub Program: BaliProgram = {
    <s: StatementSet> => s,
};

pub StatementSet: Vec<Statement> = {
    <mut s: StatementSet> <t: Statement> => { s.push(t); s },
    <mut s: StatementSet> Comment => s,
    <t: Statement> => vec![t],
    Comment => Vec::new(),
}

pub Statement: Statement = {
    "(>" <n: ConcreteFraction> <s: StatementSet> ")" => Statement::AfterFrac(n, s),
    "(<" <n: ConcreteFraction> <s: StatementSet> ")" => Statement::BeforeFrac(n, s),
    "(loop" <it: Number> <n: ConcreteFraction> <s: StatementSet> ")" => Statement::Loop(it, n, s),
    "(>>" <s: TopLevelEffectSet> ")" => Statement::After(s),
    "(<<" <s: TopLevelEffectSet> ")" => Statement::Before(s),
    <e: TopLevelEffect> => Statement::Effect(e),
};

pub TopLevelEffect: TopLevelEffect = {
    "(seq" <es: TopLevelEffectSet> ")" => TopLevelEffect::Seq(es),
    "(for" <cond: BooleanExpression> <es: TopLevelEffectSet> ")" => TopLevelEffect::For(cond, es),
    "(if" <cond: BooleanExpression> <th: TopLevelEffectSet>")" => TopLevelEffect::If(cond, th),
    <e: Effect> => TopLevelEffect::Effect(e),
}

pub TopLevelEffectSet: Vec<TopLevelEffect> = {
    <mut es: TopLevelEffectSet> <e: TopLevelEffect> => { es.push(e); es },
    <e: TopLevelEffect> => vec![e],
};

pub Effect: Effect = {
    "(def" <v: Name> <ex: Expression> ")" => Effect::Definition(v, ex),
    "(note" <vn: Expression> <vv: Expression> <vc: Expression> <vd: AbstractFraction> <vdev: Expression> ")" => Effect::Note(vn, Some(vv), Some(vc), vd, Some(vdev)),
    "(note" <vn: Expression> <vv: Expression> <vc: Expression> <vd: AbstractFraction> ")" => Effect::Note(vn, Some(vv), Some(vc), vd, None),
    "(note" <vn: Expression> <vv: Expression> <vd: AbstractFraction> <vdev: Expression> ")" => Effect::Note(vn, Some(vv), None, vd, Some(vdev)),
    "(note" <vn: Expression> <vv: Expression> <vd: AbstractFraction> ")" => Effect::Note(vn, Some(vv), None, vd, None),
    "(note" <vn: Expression> <vd: AbstractFraction> <vdev: Expression> ")" => Effect::Note(vn, None, None, vd, Some(vdev)),
    "(note" <vn: Expression> <vd: AbstractFraction> ")" => Effect::Note(vn, None, None, vd, None),
    "(note" <vn: Expression> <vdev: Expression> ")" => Effect::Note(vn, None, None, Fraction{numerator: Box::new(Expression::Value(Value::Number(1))), denominator: Box::new(Expression::Value(Value::Number(2)))}, Some(vdev)),
    "(note" <vn: Expression> ")" => Effect::Note(vn, None, None, Fraction{numerator: Box::new(Expression::Value(Value::Number(1))), denominator: Box::new(Expression::Value(Value::Number(2)))}, None),
    "(prog" <p: Expression> <c: Expression> <dev: Expression> ")" => Effect::ProgramChange(p, Some(c), Some(dev)),
    "(prog" <p: Expression> <c: Expression> ")" => Effect::ProgramChange(p, Some(c), None),
    "(prog" <p: Expression> ")" => Effect::ProgramChange(p, None, None),
    "(control" <con: Expression> <v: Expression> <c: Expression> <dev: Expression> ")" => Effect::ControlChange(con, v, Some(c), Some(dev)),
    "(control" <con: Expression> <v: Expression> <c: Expression> ")" => Effect::ControlChange(con, v, Some(c), None),
    "(control" <con: Expression> <v: Expression> ")" => Effect::ControlChange(con, v, None, None),
    "(device" <v: Expression> ")" => Effect::Device(v),
};

pub BooleanExpression: Box<BooleanExpression> = {
    "(and" <e1: BooleanExpression> <e2: BooleanExpression> ")" => Box::new(BooleanExpression::And(e1, e2)),
    "(or" <e1: BooleanExpression> <e2: BooleanExpression> ")" => Box::new(BooleanExpression::Or(e1, e2)),
    "(not" <e: BooleanExpression> ")" => Box::new(BooleanExpression::Not(e)),
    "(lt" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Lower(e1, e2)),
    "(leq" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::LowerOrEqual(e1, e2)),
    "(gt" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Greater(e1, e2)),
    "(geq" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::GreaterOrEqual(e1, e2)),
    "(==" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Equal(e1, e2)),
    "(!=" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Different(e1, e2)),
}

pub Expression: Box<Expression> = {
    "(+" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Addition(v1, v2)),
    "(*" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Multiplication(v1, v2)),
    "(-" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Subtraction(v1, v2)),
    "(/" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Division(v1, v2)),
    "(%" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Modulo(v1, v2)),
    <v: Value> => Box::new(Expression::Value(v)),
};

pub Value: Value = {
    <n: Number> => Value::Number(n),
    <v: Name> => v,
};

#[inline]
AbstractFraction: Fraction = {
    "(//" <numerator: Expression> <denominator: Expression> ")" => Fraction{numerator, denominator},
    "(" <numerator: Expression> "//" <denominator: Expression> ")" => Fraction{numerator, denominator},
};

ConcreteFraction: ConcreteFraction = {
    "(//" <numerator: Number> <denominator: Number> ")" => ConcreteFraction{signe: 1, numerator, denominator},
    "(" <numerator: Number> "//" <denominator: Number> ")" => ConcreteFraction{signe: 1, numerator, denominator},
    <denominator: Number> => {
        if denominator == 0 {
            ConcreteFraction{signe: 1, numerator: 0, denominator: 1}
        } else {
            ConcreteFraction{signe: 1, numerator: 1, denominator}
        }
    },
};

Number: i64 = <s:r"[0-9]+"> => { 
    if let Ok(v) = s.parse::<u64>() {
        (v % 128) as i64
    } else {
        0
    }
};

Name: Value = <s:r"[a-zA-Z][-a-zA-Z0-9#]*"> => Value::Variable(s.to_string());

Comment = r";.*";