use crate::compiler::bali::bali_ast::{BaliProgram, BaliContext, TopLevelEffect, Statement, Effect, Expression, Value, Fraction, ConcreteFraction, BooleanExpression, set_context_prog, set_context_effect_set};

grammar;

pub Program: BaliProgram = {
    <s: StatementSet> => s,
};

startWith = "(with";
endWith = ")";

pub WithContext: BaliContext = {
    <mut c: WithContext> "ch:" <e: Expression> => { c.channel = Some(*e); c },
    <mut c: WithContext> "dev:" <n: Number> => { c.device = Some(n); c },
    <mut c: WithContext> "v:" <e: Expression> => { c.velocity = Some(*e); c },
    <mut c: WithContext> "dur:" <f: AbstractFraction> => { c.duration = Some(f); c },
    "dev:" <n: Number> => {let mut c = BaliContext::new(); c.device = Some(n); c},
    "ch:" <e: Expression> => {let mut c = BaliContext::new(); c.channel = Some(*e); c},
    "v:" <e: Expression> => {let mut c = BaliContext::new(); c.velocity = Some(*e); c},
    "dur:" <f: AbstractFraction> => {let mut c = BaliContext::new(); c.duration = Some(f); c}
}

pub StatementSet: Vec<Statement> = {
    <mut s: StatementSet> <t: Statement> => { s.push(t); s },
    <mut s: StatementSet> startWith <c: WithContext> <ss: StatementSet> endWith => { s.extend(set_context_prog(ss, c)); s },
    startWith <c: WithContext> <s: StatementSet> endWith => set_context_prog(s, c),
    <s: StatementSet> Comment => s,
    <t: Statement> => vec![t],
    Comment => Vec::new(),
}

pub Statement: Statement = {
    "(>" <n: ConcreteFraction> <s: StatementSet> ")" => Statement::AfterFrac(n, s, BaliContext::new()),
    "(<" <n: ConcreteFraction> <s: StatementSet> ")" => Statement::BeforeFrac(n, s, BaliContext::new()),
    "(loop" <it: Number> <n: ConcreteFraction> <s: StatementSet> ")" => Statement::Loop(it, n, s, BaliContext::new()),
    "(>>" <s: TopLevelEffectSet> ")" => Statement::After(s, BaliContext::new()),
    "(<<" <s: TopLevelEffectSet> ")" => Statement::Before(s, BaliContext::new()),
    <e: TopLevelEffect> => Statement::Effect(e, BaliContext::new()),
};

pub TopLevelEffect: TopLevelEffect = {
    "(seq" <es: TopLevelEffectSet> ")" => TopLevelEffect::Seq(es, BaliContext::new()),
    "(for" <cond: BooleanExpression> <es: TopLevelEffectSet> ")" => TopLevelEffect::For(cond, es, BaliContext::new()),
    "(if" <cond: BooleanExpression> <th: TopLevelEffectSet>")" => TopLevelEffect::If(cond, th, BaliContext::new()),
    <e: Effect> => TopLevelEffect::Effect(e, BaliContext::new()),
}

pub TopLevelEffectSet: Vec<TopLevelEffect> = {
    <mut es: TopLevelEffectSet> <e: TopLevelEffect> => { es.push(e); es },
    <mut es: TopLevelEffectSet> startWith <c: WithContext> <ess: TopLevelEffectSet> endWith => { es.extend(set_context_effect_set(ess, c)); es },
    startWith <c: WithContext> <es: TopLevelEffectSet> endWith => set_context_effect_set(es, c),
    <e: TopLevelEffect> => vec![e],
};

pub Effect: Effect = {
    "(def" <v: Name> <ex: Expression> ")" => Effect::Definition(v, ex),
    "(note" <c1: WithContext> <n: Expression> <c2: WithContext> ")" => Effect::Note(n, c2.update(c1)),
    "(note" <c1: WithContext> <n: Expression> ")" => Effect::Note(n, c1),
    "(note" <n: Expression> <c2: WithContext> ")" => Effect::Note(n, c2),
    "(note" <n: Expression> ")" => Effect::Note(n, BaliContext::new()),
    "(prog" <c1: WithContext> <v: Expression> <c2: WithContext> ")" => Effect::ProgramChange(v, c2.update(c1)),
    "(prog" <c1: WithContext> <v: Expression> ")" => Effect::ProgramChange(v, c1),
    "(prog" <v: Expression> <c2: WithContext> ")" => Effect::ProgramChange(v, c2),
    "(prog" <v: Expression> ")" => Effect::ProgramChange(v, BaliContext::new()),
    "(control" <c1: WithContext> <v1: Expression> <c2: WithContext> <v2: Expression> <c3: WithContext> ")" => Effect::ControlChange(v1, v2, c3.update(c2.update(c1))),
    "(control" <c1: WithContext> <v1: Expression> <c2: WithContext> <v2: Expression> ")" => Effect::ControlChange(v1, v2, c2.update(c1)),
    "(control" <c1: WithContext> <v1: Expression> <v2: Expression> <c3: WithContext> ")" => Effect::ControlChange(v1, v2, c3.update(c1)),
    "(control" <v1: Expression> <c2: WithContext> <v2: Expression> <c3: WithContext> ")" => Effect::ControlChange(v1, v2, c3.update(c2)),
    "(control" <c1: WithContext> <v1: Expression> <v2: Expression> ")" => Effect::ControlChange(v1, v2, c1),
    "(control" <v1: Expression> <c2: WithContext> <v2: Expression> ")" => Effect::ControlChange(v1, v2, c2),
    "(control" <v1: Expression> <v2: Expression> <c3: WithContext> ")" => Effect::ControlChange(v1, v2, c3),
    "(control" <v1: Expression> <v2: Expression> ")" => Effect::ControlChange(v1, v2, BaliContext::new()),
};

pub BooleanExpression: Box<BooleanExpression> = {
    "(and" <e1: BooleanExpression> <e2: BooleanExpression> ")" => Box::new(BooleanExpression::And(e1, e2)),
    "(or" <e1: BooleanExpression> <e2: BooleanExpression> ")" => Box::new(BooleanExpression::Or(e1, e2)),
    "(not" <e: BooleanExpression> ")" => Box::new(BooleanExpression::Not(e)),
    "(lt" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Lower(e1, e2)),
    "(leq" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::LowerOrEqual(e1, e2)),
    "(gt" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Greater(e1, e2)),
    "(geq" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::GreaterOrEqual(e1, e2)),
    "(==" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Equal(e1, e2)),
    "(!=" <e1: Expression> <e2: Expression> ")" => Box::new(BooleanExpression::Different(e1, e2)),
}

pub Expression: Box<Expression> = {
    "(+" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Addition(v1, v2)),
    "(*" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Multiplication(v1, v2)),
    "(-" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Subtraction(v1, v2)),
    "(/" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Division(v1, v2)),
    "(%" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Modulo(v1, v2)),
    "(scale" <val: Expression> <old_min: Expression> <old_max: Expression> <new_min: Expression> <new_max: Expression> ")" => 
        Box::new(Expression::Scale(val, old_min, old_max, new_min, new_max)),
    "(clamp" <val: Expression> <min: Expression> <max: Expression> ")" => Box::new(Expression::Clamp(val, min, max)),
    "(min" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Min(v1, v2)),
    "(max" <v1: Expression> <v2: Expression> ")" => Box::new(Expression::Max(v1, v2)),
    "(quantize" <val: Expression> <step: Expression> ")" => Box::new(Expression::Quantize(val, step)),
    "(sine" <speed: Expression> ")" => Box::new(Expression::Sine(speed)),
    "(saw" <speed: Expression> ")" => Box::new(Expression::Saw(speed)),
    "(triangle" <speed: Expression> ")" => Box::new(Expression::Triangle(speed)),
    "(isaw" <speed: Expression> ")" => Box::new(Expression::ISaw(speed)),
    "(randstep" <speed: Expression> ")" => Box::new(Expression::RandStep(speed)),
    <v: Value> => Box::new(Expression::Value(v)),
};

pub Value: Value = {
    <n: Number> => Value::Number(n),
    <v: Name> => v,
};

#[inline]
AbstractFraction: Fraction = {
    "(//" <numerator: Expression> <denominator: Expression> ")" => Fraction{numerator, denominator},
    "(" <numerator: Expression> "//" <denominator: Expression> ")" => Fraction{numerator, denominator},
};

ConcreteFraction: ConcreteFraction = {
    "(//" <numerator: Number> <denominator: Number> ")" => ConcreteFraction{signe: 1, numerator, denominator},
    "(" <numerator: Number> "//" <denominator: Number> ")" => ConcreteFraction{signe: 1, numerator, denominator},
    <denominator: Number> => {
        if denominator == 0 {
            ConcreteFraction{signe: 1, numerator: 0, denominator: 1}
        } else {
            ConcreteFraction{signe: 1, numerator: 1, denominator}
        }
    },
};

Number: i64 = <s:r"[0-9]+"> => { 
    if let Ok(v) = s.parse::<u64>() {
        (v % 128) as i64
    } else {
        0
    }
};

Name: Value = <s:r"[a-zA-Z][-a-zA-Z0-9#]*"> => Value::Variable(s.to_string());

Comment = r";.*";